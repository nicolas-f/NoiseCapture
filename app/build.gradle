apply plugin: 'com.android.application'

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

def getAvailableLocales() {
    new File("app/src/main/res").list(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            return name.startsWith("values-") && new File(new File(dir,name), "strings.xml").exists();
        }
    }).collect() { fold -> fold.substring("values-".length())}.join(",")
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    def signingFilePath = System.getProperty("user.home") + "/.idea/signing.gradle"
    if (new File(signingFilePath).exists()) {
        apply from: signingFilePath
    }
    defaultConfig {
        applicationId "org.noise_planet.noisecapture"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 51
        versionName "1.2.15"
        // Store build date in apk
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GITHASH", "\"${getCheckedOutGitCommitHash().toString()}\""
        buildConfigField "String", "SUPPORTEDLOCALES",  "\"${getAvailableLocales()}\""
        // Enabling multidex support.
        multiDexEnabled false

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (new File(signingFilePath).exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            if (new File(signingFilePath).exists()) {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

// For using the MPAndroidChart package
// https://github.com/PhilJay/MPAndroidChart
// Apache License, Version 2.0
task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://repo1.maven.org/maven2/"
    }
    maven {
        url "https//repo.orbisgis.org"
    }
    mavenLocal()
}

dependencies {
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    implementation 'org.slf4j:slf4j-simple:1.7.12'
    implementation name: 'org/noise-planet/qrtone/0.3.0/qrtone-0.3.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.10'

    // multithreaded FFT for realtime visualisation of spectrum only
    implementation 'com.github.wendykierp:JTransforms:3.1'
    implementation 'org.apache.commons:commons-math3:3.5'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:animated-vector-drawable:28.0.0'
    implementation 'com.nhaarman.supertooltips:library:3.0.0'
    //compile 'com.android.support:multidex:1.0.0'
    // Testing-only dependencies
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // unit test
    testImplementation group: 'org.robolectric', name: 'robolectric', version: '4.3.1'
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'com.googlecode.soundlibs', name: 'jorbis', version: '0.0.17.4'
    implementation project(':sosfilter')
}
